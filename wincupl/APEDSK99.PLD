Name     APEDSK99 ;
PartNo   12o ;
Date     3/10/2020 ;
Revision 01 ;
Designer Jochen Buur ;
Company  Burell Group ;
Assembly None ;
Location WLG NZ ;
Device   G16V8 ;

/* input pins */
PIN  2 = AD_A13 ;				/* TMS9900  A0 						*/
PIN  3 = AD_A14 ;				/* TMS9900  A1						*/
PIN  4 = AD_A15 ;				/* TMS9900  A2						*/
PIN  5 = AD_A0 ;				/* TMS9900 A15						*/
PIN  6 = WE ;					/* TMS9900 WE*						*/
PIN  7 = MEMEN ;				/* TMS9900 MEMEN* 						*/
PIN  8 = TI_BUF ;				/* Arduino TIBUF (enables 74HCT541's and GAL16V8)	*/

/* output pins */
PIN 12 = AD_CE ;				/* GAL generated chip select for IS61C512 		*/
PIN 13 = TI_INT ;				/* GAL generated Arduino interrupt for DSR write	*/
PIN 14 = AD_WE ;				/* GAL copy of TMS9900 WE* (aka tri-state buffer)	*/
PIN 15	= CE_245 ;				/* GAL generated chip select for 74LS245			*/

/* variables */
LOWMEM = !AD_A15 & !AD_A14 &  AD_A13 ;	/* 001: >2000 - >3FFF */
DSR    = !AD_A15 &  AD_A14 & !AD_A13 ;	/* 010: >4000 - >5FFF */
HIMEM1 =  AD_A15 & !AD_A14 &  AD_A13 ;	/* 101: >A000 - >BFFF */
HIMEM2 =  AD_A15 &  AD_A14 & !AD_A13 ;	/* 110: >C000 - >DFFF */
HIMEM3 =  AD_A15 &  AD_A14 &  AD_A13 ;	/* 111: >E000 - >FFFF */

/* equations */
!AD_CE, !CE_245 = !MEMEN & LOWMEM #	/* 8KB block lower 32K memory expansion, 	   74LS245 CE	*/
                  !MEMEN & DSR    #	/* 8KB DSR space, 				   74LS245 CE	*/
                  !MEMEN & HIMEM1 #	/* 8KB block 1 higher 32K memory expansion, 74LS245 CE	*/ 
                  !MEMEN & HIMEM2 #	/* 8KB block 2 higher 32K memory expansion, 74LS245 CE	*/
                  !MEMEN & HIMEM3 ;	/* 8KB block 3 higher 32K memory expansion, 74LS245 CE	*/
AD_CE.OE, CE_245.OE  = !TI_BUF ;		/* outputs enabled by TI_BUF (Arduino)	*/

AD_WE    = WE ;				/* TMS9900 WE					*/
AD_WE.OE = !TI_BUF ;				/* output enabled by TI_BUF (Arduino)	*/

!TI_INT   = !MEMEN & DSR ;			/* DSR write interrupt generation		*/ 
TI_INT.OE = !TI_BUF ;			/* output enabled by TI_BUF (Arduino)	*/




