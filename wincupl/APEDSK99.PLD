Name     APEDSK99 ;
PartNo   12o ;
Date     3/10/2020 ;
Revision 01 ;
Designer Jochen Buur ;
Company  Burell Group ;
Assembly None ;
Location WLG NZ ;
Device   G16V8MS ;


/* input pins */
PIN  2 = AD_A13 ;					/* TMS9900  A0 						*/
PIN  3 = AD_A14 ;					/* TMS9900  A1						*/
PIN  4 = AD_A15 ;					/* TMS9900  A2						*/
PIN  5 = AD_A0 ;					/* TMS9900 A15						*/
PIN  6 = WE ;						/* TMS9900 WE*						*/
PIN  7 = MEMEN ;					/* TMS9900 MEMEN* 						*/
PIN  8 = TI_BUF ;					/* Arduino TIBUF (enables 74HCT541's and GAL16V8)	*/

/* output pins */
PIN 12 = AD_CE ;					/* GAL generated chip select for IS61C512 		*/
PIN 13 = TI_INT ;					/* GAL generated Arduino flag for DSR write	*/
PIN 14 = AD_WE ;					/* GAL copy of TMS9900 WE* (aka tri-state buffer)	*/
PIN 15	= CE_245 ;					/* GAL generated chip select for 74LS245			*/

/* variables */
LOWMEM	= !MEMEN & !AD_A15 & !AD_A14 &  AD_A13 ;	/* 001: low memory expansion 2000-3FFF  			*/
DSR	= !MEMEN & !AD_A15 &  AD_A14 & !AD_A13 ;	/* 010: DSR memory                      			*/
HIMEM1	= !MEMEN &  AD_A15 & !AD_A14 &  AD_A13 ;	/* 101: high memory expansion A000-BFFF 			*/
HIMEM2	= !MEMEN &  AD_A15 &  AD_A14 & !AD_A13 ;	/* 110: high memory expansion C000-DFFF 			*/
HIMEM3	= !MEMEN &  AD_A15 &  AD_A14 &  AD_A13 ;	/* 111: high memory expansion E000-FFFF 			*/

/* equations */
!AD_CE, !CE_245  = LOWMEM # DSR # HIMEM1 # HIMEM2 # HIMEM3;
AD_CE.OE  = !TI_BUF ;
CE_245.OE = !TI_BUF ;

AD_WE    = WE ;
AD_WE.OE = !TI_BUF ;					/* WE* from TMS9900						*/

!TI_INT  = DSR & !WE & !AD_A0 ;			/* write to DSR Arduino interrupt  			*/
TI_INT.OE = !TI_BUF ; 




